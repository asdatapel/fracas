#version 330 core

out vec4 FragColor;

in vec3 frag_world_pos;
in vec2 frag_uv;
in vec2 frag_text_uv;
in vec3 frag_normal;

uniform float ripple_t;
uniform sampler2D num_tex;
uniform vec3 camera_position;

#include pbr_uniforms  
#include pbr_env_uniforms  
#include light_uniforms

#include consts  
#include pbr

// https://easings.net/#easeOutElastic
float easeOutElastic(float t) {
    float c4 = (2 * 3.1415) / 3;

    t = clamp(t, 0, 1);
    return pow(2, -10 * t) * sin((t * 10 - 0.75) * c4) + 1;
}

vec4 ripple(float d, float thickness, vec3 color, float alpha)
{
    float distance_from_center = length((frag_text_uv - vec2(0.5, 0.25)) * vec2(1, 0.5));
    float distance_from_ripple = distance_from_center - d;
    return smoothstep(thickness, thickness * 3 / 10, abs(distance_from_ripple)) * vec4(color, alpha);
}

vec4 number_and_ripple()
{
    vec4 ripple_color = ripple(ripple_t, 0.01 + 0.1 * ripple_t * ripple_t, vec3(.1, .1, .9), 1)
                + ripple(0.3 * sqrt(1 - pow((ripple_t - 0.25) - 1, 2)) ,  0.1 * ripple_t, vec3(.05, .05, .45) * 2, 1 - (ripple_t));

    vec2 scale = vec2(easeOutElastic(ripple_t) * 3, easeOutElastic(ripple_t) * 2);
    vec2 center = vec2(0.5, 0.25);
    vec2 scaled_uv = (frag_text_uv - center) * scale + vec2(0.5, 0.5);
    vec4 num = texture(num_tex, scaled_uv);
    return mix(ripple_color * 2, num, num.a);
}

void main()
{
    float metal = texture(metal_texture, frag_uv).x;
    vec3 albedo = texture(albedo_texture, frag_uv).xyz;
    vec3 normal = texture(normal_texture, frag_uv).xyz;
    float roughness = max(texture(roughness_texture, frag_uv).x, 0.001); // zero roughness bad
    float ao = texture(ao_texture, frag_uv).x;

    vec4 overlay = vec4(0, 0, 0, clamp(-ripple_t, 0, 1) / 2);
    if (ripple_t >= 0)
    {
        vec4 masked_overlay = (1 - step(0.6, metal)) * number_and_ripple();
        vec4 blended_overlay = mix(vec4(0), masked_overlay, masked_overlay.a);
        overlay = blended_overlay;
    }

    normal = normalize(frag_normal);
    vec3 light_out = {0, 0, 0};
    for (int i = 0; i < num_lights; i++){
        light_out += PbrLight(frag_world_pos, normal, camera_position, albedo, roughness, metal, spot_lights[i]); 
    }
    vec3 ambient = PbrAmbient(irradiance, env_map, brdf, normal, albedo, roughness, metal, ao);

    vec3 color = mix(light_out + ambient, vec3(overlay) * 5, overlay.a);
    FragColor = vec4(color, 1.0);
} 